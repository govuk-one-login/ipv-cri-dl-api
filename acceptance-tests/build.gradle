plugins {
	id 'java'
	id 'io.freefair.aspectj.post-compile-weaving' version '8.12.1'
}

defaultTasks 'clean', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}


dependencies {
	var aws_powertools_version = "1.20.1"
	var java_cucumber_version = "7.23.0"
	var dependencyVersions = [

		cri_common_lib_version             : "6.6.0",

		// CRI_LIB aws
		aws_sdk_version                    : "2.36.1",
		aws_lambda_events_version          : "3.16.0",

		// CRI_LIB nimbus
		nimbusds_oauth_version             : "11.29.1",

		// CRI_LIB powertools
		aws_powertools_logging_version     : "${aws_powertools_version}",
		aws_powertools_metrics_version     : "${aws_powertools_version}",
		aws_powertools_parameters_version  : "${aws_powertools_version}",

		// ---------------------------------------------------------
		// AC Test Dependencies (DL CRI)
		// ---------------------------------------------------------

		aws_lambda_core_version            : "1.3.0",
		// Object mapper
		jackson_version                    : "2.20.0",

		// Code weaving (powertools)
		aspectjrt_version                  : "1.9.22.1",

		// Test
		junit_platform                     : "1.13.4",
		junit_version                      : "5.13.4",
		hamcrest_version                   : "2.2",
		mockito_version                    : "5.17.0",
		// testFixturesImplementation

		// acceptance tests Implementation
		// Update these together
		cucumber_version                   : "${java_cucumber_version}",
		selenium_version                   : "4.35.0",
		axe_core_selenium_version          : "4.10.2",
		webdrivermanager_version           : "6.3.2",

		// acceptance tests testImplementation
		rest_assured_version               : "5.5.6",
		cucumber_junit_version             : "${java_cucumber_version}"
	]

	implementation platform("software.amazon.awssdk:bom:${dependencyVersions.aws_sdk_version}"),
			platform("com.fasterxml.jackson:jackson-bom:${dependencyVersions.jackson_version}"),
			"org.aspectj:aspectjrt:${dependencyVersions.aspectjrt_version}",
			"com.fasterxml.jackson.datatype:jackson-datatype-jsr310",
			"com.fasterxml.jackson.datatype:jackson-datatype-jdk8",
			"com.fasterxml.jackson.core:jackson-databind",
			"com.fasterxml.jackson.core:jackson-annotations",
			"com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}",
			platform("org.junit:junit-bom:${dependencyVersions.junit_version}"),
			"org.junit.platform:junit-platform-launcher:${dependencyVersions.junit_platform}",
			"org.junit.jupiter:junit-jupiter-engine",
			"org.junit.jupiter:junit-jupiter-api",
			"org.junit.jupiter:junit-jupiter-params",
			"org.mockito:mockito-junit-jupiter:${dependencyVersions.mockito_version}",
			"org.mockito:mockito-core:${dependencyVersions.mockito_version}",
			"org.hamcrest:hamcrest:${dependencyVersions.hamcrest_version}",
			"com.google.code.gson:gson:${dependencyVersions.gson_version}",
			"org.json:json:20240303"


	implementation "io.cucumber:cucumber-java:${dependencyVersions.cucumber_version}",
			"org.seleniumhq.selenium:selenium-java:${dependencyVersions.selenium_version}",
			"io.github.bonigarcia:webdrivermanager:${dependencyVersions.webdrivermanager_version}",
			"com.deque.html.axe-core:selenium:${dependencyVersions.axe_core_selenium_version}"

	testImplementation "io.rest-assured:rest-assured:${dependencyVersions.rest_assured_version}",
			"io.cucumber:cucumber-junit:${dependencyVersions.cucumber_junit_version}"

	aspect "software.amazon.lambda:powertools-logging:${dependencyVersions.aws_powertools_logging_version}",
			"software.amazon.lambda:powertools-metrics:${dependencyVersions.aws_powertools_metrics_version}",
			"software.amazon.lambda:powertools-parameters:${dependencyVersions.aws_powertools_parameters_version}",
			"org.aspectj:aspectjrt:${dependencyVersions.aspectjrt_version}"
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

test {
	useJUnitPlatform()
	systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
}

//*************************************//
//                                     //
//    Test tasks                       //
//                                     //
//*************************************//

task cucumber() {
	dependsOn assemble, testClasses

	doLast {
		javaexec {
			systemProperties = [
				'cucumber.tags': "${tags}"
			]
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--plugin',
				'html:target/cucumber-report/index.html',
				'--tags',
				"${tags}",
				'--glue',
				'gov/di_ipv_drivingpermit/step_definitions',
				'src/test/resources' ,
				'--plugin',
				'html:build/test-results/cucmber.html',
				'--plugin',
				'json:build/test-results/cucumber.json'
			]
		}
	}
}

task smokeTestBuild() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--plugin',
				'html:target/cucumber-report/index.html',
				'--glue',
				'gov/di_ipv_drivingpermit/step_definitions',
				'src/test/resources/features/',
				'--tags',
				'@smoke'
			]
		}
	}
}

task smokeTestStaging() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--glue',
				'gov/di_ipv_drivingpermit/step_definitions',
				'src/test/resources/features/',
				'--tags',
				'@staging'
			]
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint" << "-Xlint:-processing"
}

apply plugin: 'java'
