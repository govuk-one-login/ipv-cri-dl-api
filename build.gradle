plugins {
	id "java"
	id "idea"
	id "org.sonarqube" version "4.0.0.2929"
	id "io.freefair.lombok" version "8.4"
	id "jacoco"
	id "jacoco-report-aggregation"
	id "com.diffplug.spotless" version "6.23.3"
	id "nebula.lint" version "19.0.1"
}

defaultTasks 'clean', 'spotlessApply', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

ext {
	dependencyVersions = [
		// Implementation
		cri_common_lib_version             : "1.5.0",
		nimbusds_oauth_version             : "10.8",
		nimbusds_jwt_version               : "9.31",
		aws_sdk_version                    : "2.20.18",
		aws_lambda_core_version            : "1.2.1",
		aws_lambda_events_version          : "3.11.0",
		jackson_version                    : "2.15.0",
		gson_version                       : "2.8.9",
		httpcomponents_core_version        : "4.4.16",
		httpcomponents_client_version      : "4.5.14",
		passay_version                     : "1.6.4",
		// Aspect
		aws_powertools_logging_version     : "1.12.2",
		aws_powertools_metrics_version     : "1.12.2",
		aws_powertools_parameters_version  : "1.9.0",
		aspectjrt_version                  : "1.9.21",
		// Compile/Annotation
		lombok_version                     : "1.18.22",
		// Test
		junit_version                      : "5.8.2",
		hamcrest_version                   : "2.2",
		mockito_version                    : "4.3.1",
		wiremock_version                   : "2.35.0",
		webcompere_version                 : "2.0.2",
		bouncy_castle_version              : "1.68",
	]

	// Sets the version used on the lambda + lib (ac tests have separate dependencies)
	javaCompatibility = [
		source : JavaVersion.VERSION_17,
		target : JavaVersion.VERSION_17
	]

	// Code Coverage (Lines/Branches) cannot be below this value on a per sub project basis
	// Requires review coverage is higher than this
	minUnitTestLineCoverage = 0.69
	minUnitTestBranchCoverage = 0.58
}

sonar {
	properties {
		property "sonar.projectKey", "ipv-cri-dl-api"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.coveragePlugin", "jacoco"
		property "sonar.coverage.jacoco.xmlReportPath", "${buildDir}/reports/jacoco/reports/reports.xml"
	}
}

// This generates an aggregate test report at "${buildDir}/reports/jacoco/reports/reports.xml"
reporting {
	reports {
		reports(JacocoCoverageReport) {
			testType = TestSuiteType.UNIT_TEST
		}
	}
}

dependencies {
	jacocoAggregation project(':lib'),
			project(':lib-dva'),
			project(':lib-dvla'),
			project("lambdas:drivingpermitcheck"),
			project("lambdas:issuecredential"),
			project("lambdas:certexpiryreminder"),
			project("lambdas:passwordRenewal"),
			project("lambdas:apiKeyRenewal")
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

subprojects {
	apply plugin: 'org.sonarqube'
	apply plugin: 'io.freefair.lombok'
	apply plugin: "nebula.lint"
	//gradleLint {
	//	rules=['unused-dependency']
	//}

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
		//flatDir {
		//	dirs '<Location of your projects absolute path>/di-ipv-cri-lib/build/libs'
		//}
	}

	plugins.withId('java') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	plugins.withId('java-library') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	task allDeps(type: DependencyReportTask) {}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}
