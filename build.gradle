plugins {
	id "java"
	id "idea"
	id "org.sonarqube" version "4.0.0.2929"
	id "io.freefair.lombok" version "8.4"
	id "jacoco"
	id "jacoco-report-aggregation"
	id "com.diffplug.spotless" version "6.23.3"
	id "nebula.lint" version "19.0.1"
}

defaultTasks 'clean', 'spotlessApply', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

ext {
	dependencyVersions = [

		// ---------------------------------------------------------
		// cri_common_lib dependencies should match the ipv-cri-lib version
		// Workaround until dependency resolution is fixed.
		// ---------------------------------------------------------
		cri_common_lib_version             : "1.5.6",

		// CRI_LIB aws
		aws_sdk_version                    : "2.20.162",
		aws_lambda_events_version          : "3.11.0",
		aws_embedded_metrics_version       : "1.0.6",

		// CRI_LIB nimbus
		nimbusds_oauth_version             : "11.2",
		nimbusds_jwt_version               : "9.36",

		// CRI_LIB powertools
		aws_powertools_logging_version     : "1.12.0",
		aws_powertools_metrics_version     : "1.12.0",
		aws_powertools_parameters_version  : "1.12.0",

		// ---------------------------------------------------------
		// DL CRI Dependencies
		// ---------------------------------------------------------

		// AWS  aws-lambda-java-libs see https://github.com/aws/aws-lambda-java-libs
		aws_lambda_core_version            : "1.2.1",
		// Object mapper
		jackson_version                    : "2.15.0",
		// GSON used in DCS/DVA pathway remove with DCS removal rework to jackson
		gson_version						: "2.8.9",

		// Code weaving (lombok+powertools)
		aspectjrt_version                  : "1.9.21",

		// CRI Apache HTTP Client see https://hc.apache.org/httpcomponents-client-4.5.x/current/httpclient/dependencies.html
		httpcomponents_core_version        : "4.4.16",
		httpcomponents_client_version      : "4.5.14",

		// password renewal lambda
		passay_version                     : "1.6.4",

		// Test
		junit_version                      : "5.10.1",
		hamcrest_version                   : "2.2",
		mockito_version                    : "5.2.0",
		webcompere_version                 : "2.0.2",

		// testFixturesImplementation

		// Contract Tests
		pact_provider_version              : "4.6.4",
		slf4j_log4j12_version              : "2.0.12" // For contract test debug
	]

	// Sets the version used on the lambda + lib (ac tests have separate dependencies)
	javaCompatibility = [
		source : JavaVersion.VERSION_17,
		target : JavaVersion.VERSION_17
	]

	// Code Coverage (Lines/Branches) cannot be below this value on a per sub project basis
	// Requires review coverage is higher than this
	minUnitTestLineCoverage = 0.80
	minUnitTestBranchCoverage = 0.80
}

sonar {
	properties {
		property "sonar.projectKey", "ipv-cri-dl-api"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.coveragePlugin", "jacoco"
		property "sonar.coverage.jacoco.xmlReportPath", "${buildDir}/reports/jacoco/reports/reports.xml"
	}
}

// This generates an aggregate test report at "${buildDir}/reports/jacoco/reports/reports.xml"
reporting {
	reports {
		reports(JacocoCoverageReport) {
			testType = TestSuiteType.UNIT_TEST
		}
	}
}

dependencies {
	jacocoAggregation project(':lib'),
			project(':lib-dva'),
			project(':lib-dvla'),
			project("lambdas:drivingpermitcheck"),
			project("lambdas:issuecredential"),
			project("lambdas:certexpiryreminder"),
			project("lambdas:passwordRenewal")
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

subprojects {
	apply plugin: 'org.sonarqube'
	apply plugin: 'io.freefair.lombok'
	apply plugin: "nebula.lint"
	//gradleLint {
	//	rules=['unused-dependency']
	//}

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
		//flatDir {
		//	dirs '<Location of your projects absolute path>/di-ipv-cri-lib/build/libs'
		//}
	}

	plugins.withId('java') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	plugins.withId('java-library') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	task allDeps(type: DependencyReportTask) {}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}
