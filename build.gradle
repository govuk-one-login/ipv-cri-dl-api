plugins {
	id "java"
	id "idea"
	id "org.sonarqube" version "7.0.1.6134"
	id "io.freefair.lombok" version "8.12.1"
	id 'io.freefair.aspectj.post-compile-weaving' version '8.12.1'
	id "jacoco"
	id "jacoco-report-aggregation"
	id "com.diffplug.spotless" version "8.0.0"
	id "nebula.lint" version "20.5.6"
}

defaultTasks 'clean', 'spotlessApply', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

ext {
	aws_powertools_version = "1.18.0"
	dependencyVersions = [

		cri_common_lib_version             : "7.0.0",

		// AWS SDK
		aws_sdk_version                    : "2.34.3",
		aws_lambda_events_version          : "3.16.0",

		// Nimbus Oauth
		nimbusds_oauth_version             : "11.29.1",

		// CRI_LIB powertools
		aws_powertools_logging_version     : "${aws_powertools_version}",
		aws_powertools_metrics_version     : "${aws_powertools_version}",
		aws_powertools_parameters_version  : "${aws_powertools_version}",

		// ---------------------------------------------------------
		// DL CRI Dependencies
		// ---------------------------------------------------------

		// AWS  aws-lambda-java-libs see https://github.com/aws/aws-lambda-java-libs
		aws_lambda_core_version            : "1.3.0",
		// Jackson Addons/ needs to track the aws sdk version of jackson
		jackson_version                    : "2.20.0",

		// GSON used in DCS/DVA pathway remove with DCS removal rework to jackson
		gson_version                       : "2.8.9",

		// Code weaving (powertools+lombok)
		aspectjrt_version                  : "1.9.22.1",

		// Open telemetry
		opentelemetry_bom_alpha_version    : "2.20.1-alpha",

		// CRI Apache HTTP Client see https://hc.apache.org/httpcomponents-client-4.5.x/current/httpclient/dependencies.html
		httpcomponents_core_version        : "4.4.16",
		httpcomponents_client_version      : "4.5.14",

		// password renewal lambda
		passay_version                     : "1.6.4",

		bouncycastle_bcpkix_version        : "1.80",

		// Test
		junit_platform                     : "6.0.1",
		junit_version                      : "6.0.1",
		hamcrest_version                   : "2.2",
		mockito_version                    : "5.17.0",
		webcompere_version                 : "2.1.8",
		wiremock_version                   : "3.13.2",

		// testFixturesImplementation

		// Contract Tests
		pact_provider_version              : "4.6.18",
		slf4j_log4j12_version              : "2.0.17", // For contract test debug
	]

	// Sets the version used on the lambda + lib (ac tests have separate dependencies)
	javaCompatibility = [
		source : JavaVersion.VERSION_17,
		target : JavaVersion.VERSION_17
	]

	// Code Coverage (Lines/Branches) cannot be below this value on a per sub project basis
	// Requires review coverage is higher than this
	minUnitTestLineCoverage = 0.80
	minUnitTestBranchCoverage = 0.80
}

sonar {
	properties {
		property "sonar.projectKey", "ipv-cri-dl-api"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.coveragePlugin", "jacoco"
		property "sonar.coverage.jacoco.xmlReportPath", layout.buildDirectory.file("reports/jacoco/reports/reports.xml")
	}
}

// This generates an aggregate test report at "${buildDir}/reports/jacoco/reports/reports.xml"
reporting {
	reports {
		reports(JacocoCoverageReport) {
			testSuiteName = "test"
		}
	}
}

dependencies {
	jacocoAggregation project(':lib'),
			project(':lib-dva'),
			project(':lib-dvla'),
			project("lambdas:drivingpermitcheck"),
			project("lambdas:issuecredential"),
			project("lambdas:certexpiryreminder"),
			project("lambdas:passwordRenewal"),
			project("lambdas:personInfo")
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.19.2").aosp().formatJavadoc(true)
		importOrder "", "javax", "java", "\\#"
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
		removeUnusedImports()
		forbidWildcardImports()
		formatAnnotations()
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
	shell {
		target '*.sh'
		trimTrailingWhitespace()
		endWithNewline()
	}
	gherkin {
		target 'acceptance-tests/src/test/resources/**/*.feature'
		gherkinUtils()
		endWithNewline()
	}
	flexmark {
		target '**/*.md'
		flexmark()
		endWithNewline()
	}
	format 'misc', {
		target '.gitignore', '.dockerignore', '.sdkmanrc'
		trimTrailingWhitespace()
		leadingSpacesToTabs(4)
		endWithNewline()
	}
}

subprojects {
	apply plugin: 'org.sonarqube'
	apply plugin: 'io.freefair.lombok'
	apply plugin: 'io.freefair.aspectj.post-compile-weaving'
	apply plugin: "nebula.lint"

	//gradleLint {
	//	rules=['unused-dependency']
	//}
	configurations.configureEach {

		resolutionStrategy {
			// Remove once pact pulls in this or a newer version
			force 'org.apache.tika:tika-core:3.2.3'
		}

		// These clients are excluded as we are using the aws-crt client exclusively
		// https://aws.amazon.com/blogs/developer/tuning-the-aws-java-sdk-2-x-to-reduce-startup-time/
		exclude group:"software.amazon.awssdk", module: "apache-client"
		exclude group:"software.amazon.awssdk", module: "netty-nio-client"
		exclude group:"software.amazon.awssdk", module: "url-connection-client"

		// Exclude netty in general
		exclude group: "io.netty", module: "*"
		exclude group: "io.grpc", module: "grpc-netty"
	}

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
		//flatDir {
		//	dirs '<Location of your projects absolute path>/di-ipv-cri-lib/build/libs'
		//}
	}

	plugins.withId('java') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	plugins.withId('java-library') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	plugins.withId('java-test-fixtures') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	tasks.withType(JavaCompile).configureEach {
		options.compilerArgs << "-Xlint" << "-Xlint:-processing"
	}

	task allDeps(type: DependencyReportTask) {}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}
