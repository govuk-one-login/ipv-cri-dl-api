import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
	id "java-library"
	id "idea"
	id "jacoco"
	id 'java-test-fixtures'
}

configurations.all {
	// https://aws.amazon.com/blogs/developer/tuning-the-aws-java-sdk-2-x-to-reduce-startup-time/
	exclude group:"software.amazon.awssdk", module: "apache-client"
	exclude group:"software.amazon.awssdk", module: "netty-nio-client"
	exclude group:"software.amazon.awssdk", module: "url-connection-client"
}

dependencies {
	api project(":lib")

	aspect "software.amazon.lambda:powertools-logging:${dependencyVersions.aws_powertools_logging_version}",
			"software.amazon.lambda:powertools-metrics:${dependencyVersions.aws_powertools_metrics_version}",
			"software.amazon.lambda:powertools-parameters:${dependencyVersions.aws_powertools_parameters_version}"

	testImplementation testFixtures(project(":lib")),
			platform("org.junit:junit-bom:${dependencyVersions.junit_version}"),
			"org.junit.platform:junit-platform-launcher:${dependencyVersions.junit_platform}",
			"org.junit.jupiter:junit-jupiter-engine",
			"org.junit.jupiter:junit-jupiter-api",
			"org.junit.jupiter:junit-jupiter-params",
			"org.mockito:mockito-junit-jupiter:${dependencyVersions.mockito_version}",
			"org.mockito:mockito-core:${dependencyVersions.mockito_version}",
			"org.hamcrest:hamcrest:${dependencyVersions.hamcrest_version}",
			"uk.org.webcompere:system-stubs-core:${dependencyVersions.webcompere_version}",
			"uk.org.webcompere:system-stubs-jupiter:${dependencyVersions.webcompere_version}"

	testFixturesImplementation "uk.gov.account:cri-common-lib:${dependencyVersions.cri_common_lib_version}"

	compileOnly "org.projectlombok:lombok:${dependencyVersions.lombok_version}"
	annotationProcessor "org.projectlombok:lombok:${dependencyVersions.lombok_version}"
}

tasks.named("jar") {
	manifest {
		attributes("Implementation-Title": project.name,
		"Implementation-Version": project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}

tasks.withType(Test) {
	testLogging {
		events TestLogEvent.FAILED,
				TestLogEvent.PASSED,
				TestLogEvent.SKIPPED

		exceptionFormat TestExceptionFormat.FULL
		showExceptions true
		showCauses true
		showStackTraces true

		afterSuite { suite, result ->
			if (!suite.parent) {
				def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
				def startItem = "|  ", endItem = "  |"
				def repeatLength = startItem.length() + output.length() + endItem.length()
				println("\n" + ("-" * repeatLength) + "\n" + startItem + output + endItem + "\n" + ("-" * repeatLength))
			}
		}
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				minimum = minUnitTestLineCoverage
			}
			limit {
				counter = 'BRANCH'
				minimum = minUnitTestBranchCoverage
			}
		}
	}
}
